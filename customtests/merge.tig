let 

 type any = {any : int}
 var buffer := tigerGetchar()

function readint(any: any) : int =
 let var i := 0
     function isdigit(s : string) : int = 
		  ord(buffer)>=ord("0") & ord(buffer)<=ord("9")
     function skipto() =
     (
         while stringcmp(buffer, " ")=0 | stringcmp(buffer, "\n")=0
         do (
         buffer := tigerGetchar()
         )
     )
  in 
     skipto();
     any.any := isdigit(buffer);

     while isdigit(buffer)
       do (i := i*10+ord(buffer)-ord("0"); 
          buffer := tigerGetchar()
       );
     i
 end

 type list = {first: int, rest: list}

 function readlist() : list =
    let var any := any{any=0}
        var i := readint(any)
     in 
     if any.any
         then list{first=i,rest=readlist()}
         else nil
    end

 function merge(a: list, b: list) : list =
   if a=nil then b
   else if b=nil then a
   else if a.first < b.first 
      then list{first=a.first,rest=merge(a.rest,b)}
      else list{first=b.first,rest=merge(a,b.rest)}

 function printlist(l: list) =
   if l=nil then printStr("\n")
   else (printInt(l.first); printStr(" "); printlist(l.rest))

   var list1 := readlist()
   var list2 := (buffer:=tigerGetchar(); readlist())


  /* BODY OF MAIN PROGRAM */
 in 
 printlist(merge(list1,list2))
end

